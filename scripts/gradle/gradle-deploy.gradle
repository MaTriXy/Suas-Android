apply plugin: 'maven'
apply plugin: 'signing'
apply from: '../scripts/gradle/gradle-commons.gradle'

ext {
    SDK_GROUP_ID = 'com.zendesk.suas'
    SDK_URL = 'https://github.com/zendesk/Suas-Android'
    PROJECT_NAME = 'Suas'
    PROJECT_DESCRIPTION = 'Unidirectional data flow architecture implementation for Android'
    SDK_VERSION_NAME = rootProject.ext.versionName
    SDK_ARTIFACT_ID = project.ext.artifactId
    SDK_PACKAGING = project.ext.packaging
    JAVA_DOC_TITLE = project.ext.javadocTitle
    POM_DEVELOPER_ID = 'zendesk'
    POM_DEVELOPER_NAME = 'Zendesk, Inc.'

    repoUsername = getConfigurationParameter("zenSonatypeUsername")
    repoPassword = getConfigurationParameter("zenSonatypePassword")
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: repoUsername, password: repoPassword)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: repoUsername, password: repoPassword)
                }

                pom.project {
                    name PROJECT_NAME
                    description PROJECT_DESCRIPTION
                    groupId SDK_GROUP_ID
                    artifactId SDK_ARTIFACT_ID
                    packaging SDK_PACKAGING
                    version SDK_VERSION_NAME
                    url SDK_URL

                    scm {
                        url SDK_URL
                        connection 'scm:git:https://github.com/zendesk/Suas-Android.git'
                        developerConnection 'scm:git:git@github.com:zendesk/Suas-Android.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() }
        sign configurations.archives
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    tasks.withType(Javadoc) {
        source = sourceSets.main.allJava
        options.addStringOption('Xdoclint:none', '-quiet')
        title = JAVA_DOC_TITLE
        options.links("http://docs.oracle.com/javase/8/docs/api/")
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}